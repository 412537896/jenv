import org.apache.tools.ant.filters.*

apply plugin: 'groovy'
apply plugin: 'idea'

defaultTasks 'clean', 'prepareResources', 'prepareCommands', 'prepareJenvCommands', 'assembleArchive'

ext.installDir = "${System.getProperty('user.home')}/.jenv"

loadConfiguration()

repositories {
  mavenCentral()
}

configurations {
  sshAntTask
}

dependencies {
  groovy group: 'org.codehaus.groovy', name: 'groovy', version: '2.0.5'
  groovy group: 'org.apache.ant', name: 'ant', version: '1.8.4'
  sshAntTask 'org.apache.ant:ant-jsch:1.8.4'
}

def loadConfiguration() {
  def environment = hasProperty('env') ? env : 'dev'
  ext.environment = environment
  println "Environment is set to: $environment"

  def configFile = file('config.groovy')
  def config = new ConfigSlurper(environment).parse(configFile.toURL())
  ext.config = config
}

task prepareResources(type: Copy) {
  from 'src/main/resources'
  into 'build/jenv'
  include '**/*'
  filter(ReplaceTokens, tokens: [JENV_VERSION: config.jenvVersion, JENV_SERVICE: config.jenvService])
}

task prepareCommands(type: Copy) {
  from 'src/main/scripts/bin'
  into 'build/jenv/bin'
  filter(ReplaceTokens, tokens: [JENV_VERSION: config.jenvVersion, JENV_SERVICE: config.jenvService])
}

task prepareJenvCommands(type: Copy, dependsOn: [prepareResources, prepareCommands]) {
  from 'src/main/scripts/commands'
  into 'build/jenv/commands'
  filter(ReplaceTokens, tokens: [JENV_VERSION: config.jenvVersion, JENV_SERVICE: config.jenvService])
}

task assembleArchive(type: Zip, dependsOn: [prepareJenvCommands]) {
  classifier = config.jenvVersion
  from "build/jenv"
  include "**/*"
}

task syncToSite(dependsOn: [assembleArchive]) {
  // sshAntTask configuration classpath.
  ant.taskdef(name: 'scp', classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
          classpath: configurations.sshAntTask.asPath)
  if (new File('build/distributions/jenv-' + config.jenvVersion + '.zip').exists()) {
    // upload install.sh
    ant.scp(keyfile: '${user.home}/.ssh/id_rsa', verbose: 'true',
            localFile: 'build/jenv/commands/install.sh',
            remoteTofile: 'linux_china@team.mvnsearch.org:/home/virtual_hosts/get.jvmtool.mvnsearch.org/index.txt')
    // upload jenv-xxxx.zip
    ant.scp(keyfile: '${user.home}/.ssh/id_rsa', verbose: 'true',
            localFile: 'build/distributions/jenv-' + config.jenvVersion + '.zip',
            remoteTodir: 'linux_china@team.mvnsearch.org:/home/virtual_hosts/get.jvmtool.mvnsearch.org/')
  }
}

task installJenv(type: Copy, dependsOn: [prepareJenvCommands]) {
  from "build/jenv"
  into installDir
}

task install(dependsOn: [clean, installJenv])
